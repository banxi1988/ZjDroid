apply plugin: 'com.android.application'


def generateBuildNumber(){
    def date = new Date()
    def formattedDate = date.format("yyyyMMdd")
    def formattedSeconds = date.format("HHmmss")
    def formatInt = formattedDate as int;
    def secondsInt = formattedSeconds as int;
    return (formatInt + secondsInt) as int
}

android {
    compileSdkVersion 23
    buildToolsVersion "23.0.1"
    useLibrary 'org.apache.http.legacy'
    defaultConfig {
        applicationId "com.android.updroid"
        minSdkVersion 16
        targetSdkVersion 23
        versionName "1.0.1"
        versionCode 3
//        ndk {
//            moduleName "dvmnative"
//            ldLibs "log"
//        }

        buildConfigField 'int', 'buildNumber', "${generateBuildNumber()}"
        buildConfigField 'String', 'APP_NAME', "\"UpDroid\""
        manifestPlaceholders = ["buildNumber":"${generateBuildNumber()}"]
        resValue 'string','app_name','UpDroid'
        resValue 'string','author','banxi'
    }

    productFlavors{
        xidroid{
            applicationId "com.android.xidroid"
            versionName "1.0.3"
            versionCode 5
            buildConfigField 'String', 'APP_NAME', "\"XiDroid\""
            resValue 'string','app_name','XiDroid'
        }
        updroid{

        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
        }
    }
}

android.applicationVariants.all{ variant ->
    variant.mergeResources.doLast{
        def buildNumber = generateBuildNumber()
        println "Variant DirName "+variant.dirName
        def valuesFilePath = "${buildDir}/intermediates/res/merged/${variant.dirName}/values/values.xml"
        println "Values File Path "+valuesFilePath
        File valuesFile = file(valuesFilePath)
        String content = valuesFile.getText('UTF-8')
        content = content.replaceAll(/_devBuild_/,"${buildNumber}")
        valuesFile.write(content,'UTF-8')
    }
}

dependencies {
    provided fileTree(dir: 'libs-provided', include: ['*.jar'])
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile 'com.android.support:support-v4:23.1.0'
    compile 'com.google.guava:guava:18.0'
//    compile files('libs/antlr-3.5.2-complete.jar')
//    compile files('libs/commons-cli-1.2.jar')
}
